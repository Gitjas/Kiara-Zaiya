/* ============================================ *
 *                                              *
 *                 ALWAYS BLOCK                 *
 *                                              *
 * ============================================ */

// Concept borrowed from DavidW's Immutability and encapsulation in mod design ( https://github.com/Gibberlings3/immutable )

/* =================================================================== *
 *  This code only runs once, even if you install lots of components.  *
 * =================================================================== */

ACTION_IF !VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

	SILENT

// Isaya: the IDS files included in the mod are identical to those of ToB with the latest patch. In principle, they should not be overwritten unless the official ToB patch is not installed.
/* Gwendolyne: DEPRECATED as of v2.0.0. Removed action, trigger and spell .ids files overwriting and patching as ToB has been set as a prerequisite.
		ACTION_IF NOT FILE_EXISTS ~override/action.ids~ THEN BEGIN
			COPY ~Kiara-Zaiya/ids/action.ids~ ~override/action.ids~
		END
		ACTION_IF NOT FILE_EXISTS ~override/trigger.ids~ THEN BEGIN
			COPY ~Kiara-Zaiya/ids/trigger.ids~ ~override/trigger.ids~
		END
		ACTION_IF NOT FILE_EXISTS ~override/spell.ids~ THEN BEGIN
			COPY ~Kiara-Zaiya/ids/spell.ids~ ~override/spell.ids~
		END
*/

	/* -------------------------------------------------------------------------------- *
	 *    Determines which game is installed and if the game is an Enhanced Edition.    *
	 * -------------------------------------------------------------------------------- */
	OUTER_SET is_tob   = (GAME_IS ~bgt tob~) ? 1 : 0     // based on vanilla ToB engine
	OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0       // b2gee
	OUTER_SET is_eet   = (GAME_IS "eet") ? 1 : 0         // eet
	OUTER_SET is_ee    = (GAME_IS ~bg2ee eet~) ? 1 : 0   // EE game


	/* ------------------------ *
	 *    Detects other mods    *
	 * ------------------------ */

	OUTER_SET is_tobex = (FILE_EXISTS ~tobex_ini/tobexcore.ini~) ? 1 : 0  // Detects ToBEx


	/* ------------------------------------------------------- *
	 *    Sets default variables if they're not set already    *
	 * ------------------------------------------------------- */

	// Variable used to properly install specific EE games resources
	ACTION_IF (is_ee) BEGIN
		OUTER_SPRINT ee_file "_ee"
	END ELSE BEGIN
		OUTER_SPRINT ee_file ""
	END

	// Required because of non-standard BACKUP path
	OUTER_TEXT_SPRINT MOD_FOLDER "kiara-zaiya"

	// Sets variables for the external locations
	OUTER_SPRINT tra_base       "lang"
	OUTER_SPRINT marker_loc     "weidu_external/markers"
	OUTER_SPRINT ext_lang_loc   EVAL "weidu_external/lang/%MOD_FOLDER%"
	OUTER_SPRINT marker_name    EVAL "%marker_loc%/%MOD_FOLDER%_languages_installed.mrk"
	OUTER_SPRINT ds_marker_name EVAL "%marker_loc%/ds-detectable_spells.mrk"


	/* ---------------------------------- *
	 *    Creates the external folders    *
	 * ---------------------------------- */
	MKDIR "weidu_external"
	MKDIR "weidu_external/markers"
	MKDIR "weidu_external/lang"
	MKDIR "weidu_external/lang/%MOD_FOLDER%"


	/* ------------------------------------------- *
	 *    EET chapters continuity compatibility    *
	 * ------------------------------------------- */
		ACTION_IF is_eet BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END


	/* -------------------------------------------- *
	 *    Loads in any shared function libraries    *
	 * -------------------------------------------- */

	// Loads macros and functions library dealing with tooltip appending, specific EE items descriptions, and more...
	INCLUDE ~%MOD_FOLDER%/lib/gw_functions.tpa~


	/* -------------------------- *
	 *    Creates a blank file    *
	 * -------------------------- */

	<<<<<<<< .../kiara-zaiya-inlined/blank

>>>>>>>>


	/* ----------------------------------------------------------------------------------------------- *
	 *    Convert strings to UTF-8 for BGEE/BG2EE                                                      *
	 *    No exception, everything needs to be converted                                               *
	 *    kzsetup.tra contains in game texts as well as installation texts and needs to be converted   *
	 * ----------------------------------------------------------------------------------------------- */

	ACTION_IF (!FILE_EXISTS "%marker_name%") BEGIN

		COPY ~.../kiara-zaiya-inlined/blank~ ~%marker_name%~

		MKDIR "%ext_lang_loc%/%LANGUAGE%"
		COPY ~%MOD_FOLDER%/lang/%LANGUAGE%~	~%ext_lang_loc%/%LANGUAGE%~

		/* Converts tra files to UTF-8 */
		ACTION_IF is_ee BEGIN  // Do conversions (EE only)
		
			ACTION_DEFINE_ARRAY kz#noconvert BEGIN END
			ACTION_DEFINE_ARRAY kz#reload BEGIN dialogs kzsetup END
			LAF HANDLE_CHARSETS 
				INT_VAR infer_charsets  = 1
				STR_VAR tra_path        = EVAL ~%MOD_FOLDER%/lang~
                        out_path        = EVAL ~%ext_lang_loc%~
                        iconv_path      = EVAL ~%MOD_FOLDER%/lang/iconv~
                        noconvert_array = kz#noconvert
                        reload_array    = kz#reload
			END

		END

	END // of ACTION_IF (!FILE_EXISTS "%marker_name%")

	/* TRAs declared in LANGUAGE must be reloaded - by hand, because of the copying (for non-EE version */
	LOAD_TRA "%ext_lang_loc%/%LANGUAGE%/kzsetup.tra" "%ext_lang_loc%/%LANGUAGE%/dialogs.tra"

END	// of ACTION_IF !VARIABLE_IS_SET bg2_chapter BEGIN
