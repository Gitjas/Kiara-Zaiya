BACKUP ~kiara-zaiya/backup~
AUTHOR ~olryx2@yahoo.co.uk~
ASK_EVERY_COMPONENT

// Isaya: version information removed from the tra files and set here (using a new WeiDU feature)
// Version of the mod, appearing in the WeiDU.log
VERSION ~v1.6.2~

README ~kiara-zaiya/readme_KZ.chm~

ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

// Isaya: the IDS files included in the mod are identical to those of ToB with the latest patch. In principle, they should not be overwritten unless the official ToB patch is not installed.
/* Gwendolyne: removed action, trigger and spell .ids files overwriting and patching as ToB has been set as a prerequisite.
		ACTION_IF NOT FILE_EXISTS ~override/action.ids~ THEN BEGIN
			COPY ~Kiara-Zaiya/ids/action.ids~ ~override/action.ids~
		END
		ACTION_IF NOT FILE_EXISTS ~override/trigger.ids~ THEN BEGIN
			COPY ~Kiara-Zaiya/ids/trigger.ids~ ~override/trigger.ids~
		END
		ACTION_IF NOT FILE_EXISTS ~override/spell.ids~ THEN BEGIN
			COPY ~Kiara-Zaiya/ids/spell.ids~ ~override/spell.ids~
		END
*/
		/* ----------------------------------------- *
		 *    Determines which game is installed.    *
		 * ----------------------------------------- */

		OUTER_SET is_tob   = (GAME_IS ~bgt tob tutu tutu_totsc~) ? 1 : 0     // based on vanilla ToB engine
		OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0                       // b2gee
		OUTER_SET is_eet   = (GAME_IS "eet") ? 1 : 0                         // eet
		OUTER_SET is_ee    = (GAME_IS ~bg2ee eet~) ? 1 : 0                   // EE game

		/* ------------------------------------------------------- *
		 *    Sets default variables if they're not set already    *
		 * ------------------------------------------------------- */

		// Variable used to properly install specific EE games ressources
		ACTION_IF (is_ee) BEGIN
			OUTER_SPRINT ee_file "_ee"
		END ELSE BEGIN
			OUTER_SPRINT ee_file ""
		END

		/* ------------------------------------------- *
		 *    EET chapters continuity compatibility    *
		 * ------------------------------------------- */
		ACTION_IF is_eet BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END

		/* --------------------------------------------------------------------------------------------- *
		 *    Convert strings to UTF-8 for BGEE/BG2EE                                                    *
		 *    No exception, everything needs to be converted                                             *
		 *    setup.tra contains in game texts as well as installation texts and needs to be converted   *
		 * --------------------------------------------------------------------------------------------- */
		ACTION_DEFINE_ARRAY kz#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY kz#reload BEGIN dialogs kzsetup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/lang~
				noconvert_array = kz#noconvert
				reload_array = kz#reload
		END

	END

END

// AUTO_TRA tells WeiDU to use by default the TRA file having the same name as
// dialog or script being compiled in the kiara-zaiya/<language> directory,
// where <language> is the name of the language selected by the user (second word
// in the LANGUAGE definition below).
// This removes the need to use COMPILE-USING directive for the COMPILE action
// when the .d and .tra file have the same name
AUTO_TRA ~kiara-zaiya/lang/%s~

LANGUAGE ~American English~
         ~english~
         ~kiara-zaiya/lang/english/dialogs.tra~
         ~kiara-zaiya/lang/english/kzsetup.tra~

LANGUAGE ~Francais (par Elgaern, Bloody.Mary et Lothringen des d'Oghmatiques)~
         ~french~
         ~kiara-zaiya/lang/french/dialogs.tra~
         ~kiara-zaiya/lang/french/kzsetup.tra~

LANGUAGE ~Espanol (traduccion por Saemon)~
         ~spanish~
         ~kiara-zaiya/lang/spanish/dialogs.tra~
         ~kiara-zaiya/lang/spanish/kzsetup.tra~

LANGUAGE ~Deutsch (Uebersetzung von Leonardo Watson)~
         ~german~
         ~kiara-zaiya/lang/german/dialogs.tra~
         ~kiara-zaiya/lang/german/kzsetup.tra~

LANGUAGE ~Russian (Gingerrr & aerie.ru. Updated by Prowler)~
         ~russian~
         ~kiara-zaiya/lang/russian/dialogs.tra~
         ~kiara-zaiya/lang/russian/kzsetup.tra~


/* ========================================== *
 *    Kiara-Zaiya for BG2 (main component)    *
 * ========================================== */
BEGIN @2                                  // ~Kiara-Zaiya for BG2 with vanilla soundsets~
DESIGNATED 0
SUBCOMPONENT @1                           // ~Kiara-Zaiya for BG2~
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @11 // ~You do not appear to have Throne of Bhaal installed.~

OUTER_SET kz_custom = 0

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


BEGIN @3                                  // ~Kiara-Zaiya for BG2 with custom soundsets (English only)~
DESIGNATED 1
SUBCOMPONENT @1
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @11

OUTER_SET kz_custom = 1

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


/* ======================================================= *
 *    Alternate Monk High level abilities by TG Maestro    *
 * ======================================================= */
BEGIN @4                                  // ~Alternate Monk High level abilities by TG Maestro~
DESIGNATED 2
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @11 // ~You do not appear to have Throne of Bhaal installed.~

INCLUDE ~%MOD_FOLDER%/lib/monk_hla.tpa~


/* =================================================== *
 *    Original Amaralis battles in chapters 2 and 6    *
 * =================================================== */
BEGIN @5                                  // ~Original Amaralis battles~
DESIGNATED 3
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @11 // ~You do not appear to have Throne of Bhaal installed.~

INCLUDE ~%MOD_FOLDER%/lib/amaralis_battles.tpa~


/* ================================================================== *
 *    Tougher Kiara vampire                                           *
 *                                                                    *
 *  MONK LEVEL 20, DOES LEVEL DRAIN AND HAS SOME OTHER UNDEAD POWERS  *
 * ================================================================== */
BEGIN @6                                  // ~Tougher Kiara vampire~
DESIGNATED 4
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @11 // ~You do not appear to have Throne of Bhaal installed.~

COMPILE ~kiara-zaiya/vampkia/vampkia.baf~
COPY ~kiara-zaiya/vampkia/vampkiar.cre~ ~override~
	SAY NAME1 @27
	SAY NAME2 @27
COPY ~kiara-zaiya/vampkia/vampk3.itm~ ~override~


/* =========================== *
 *    Revised Suldalanessar    *
 * =========================== */
BEGIN @7                                  // ~Revised Suldalanessar~
DESIGNATED 5
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @11 // ~You do not appear to have Throne of Bhaal installed.~

INCLUDE ~%MOD_FOLDER%/lib/revised_suldalanessar.tpa~


/* =========================== *
 *    Jao and party for SOA    *
 * =========================== */
BEGIN @8                                  // ~Jao and party for SOA: another tough fight~
DESIGNATED 6
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @11 // ~You do not appear to have Throne of Bhaal installed.~

INCLUDE ~%MOD_FOLDER%/lib/jao.tpa~


/* ========================================== *
 *    Tougher Irenicus at the Tree of life    *
 * ========================================== */
BEGIN @9                                  // ~Tougher Irenicus at the Tree of life~
DESIGNATED 7
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @11 // ~You do not appear to have Throne of Bhaal installed.~

// TOUGH BUT STILL FEASIBLE
COPY ~kiara-zaiya/irentree/sujon.cre~ ~override~
COMPILE ~kiara-zaiya/irentree/sujon.baf~
