BACKUP ~kiara-zaiya/backup~
AUTHOR ~olryx2@yahoo.co.uk~
ASK_EVERY_COMPONENT

// Isaya: version information removed from the tra files and set here (using a new WeiDU feature)
// Version of the mod, appearing in the WeiDU.log
VERSION ~v1.6.2~

README ~kiara-zaiya/readme_KZ.chm~

ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

// Isaya: the IDS files included in the mod are identical to those of ToB with the latest patch. In principle, they should not be overwritten unless the official ToB patch is not installed.
/* Gwendolyne: removed action, trigger and spell .ids files overwriting and patching as ToB has been set as a prerequisite.
		ACTION_IF NOT FILE_EXISTS ~override/action.ids~ THEN BEGIN
			COPY ~Kiara-Zaiya/ids/action.ids~ ~override/action.ids~
		END
		ACTION_IF NOT FILE_EXISTS ~override/trigger.ids~ THEN BEGIN
			COPY ~Kiara-Zaiya/ids/trigger.ids~ ~override/trigger.ids~
		END
		ACTION_IF NOT FILE_EXISTS ~override/spell.ids~ THEN BEGIN
			COPY ~Kiara-Zaiya/ids/spell.ids~ ~override/spell.ids~
		END
*/
		/* ----------------------------------------- *
		 *    Determines which game is installed.    *
		 * ----------------------------------------- */

		OUTER_SET is_tob   = (GAME_IS ~bgt tob tutu tutu_totsc~) ? 1 : 0     // based on vanilla ToB engine
		OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0                       // b2gee
		OUTER_SET is_eet   = (GAME_IS "eet") ? 1 : 0                         // eet
		OUTER_SET is_ee    = (GAME_IS ~bg2ee eet~) ? 1 : 0                   // EE game

		/* ------------------------------------------------------- *
		 *    Sets default variables if they're not set already    *
		 * ------------------------------------------------------- */

		// Variable used to properly install specific EE games ressources
		ACTION_IF (is_ee) BEGIN
			OUTER_SPRINT ee_file "_ee"
		END ELSE BEGIN
			OUTER_SPRINT ee_file ""
		END

		/* ------------------------------------------- *
		 *    EET chapters continuity compatibility    *
		 * ------------------------------------------- */
		ACTION_IF is_eet BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END

		/* --------------------------------------------------------------------------------------------- *
		 *    Convert strings to UTF-8 for BGEE/BG2EE                                                    *
		 *    No exception, everything needs to be converted                                             *
		 *    setup.tra contains in game texts as well as installation texts and needs to be converted   *
		 * --------------------------------------------------------------------------------------------- */
		ACTION_DEFINE_ARRAY kz#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY kz#reload BEGIN dialogs kzsetup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/lang~
				noconvert_array = kz#noconvert
				reload_array = kz#reload
		END

	END

END

// AUTO_TRA tells WeiDU to use by default the TRA file having the same name as
// dialog or script being compiled in the kiara-zaiya/<language> directory,
// where <language> is the name of the language selected by the user (second word
// in the LANGUAGE definition below).
// This removes the need to use COMPILE-USING directive for the COMPILE action
// when the .d and .tra file have the same name
AUTO_TRA ~kiara-zaiya/lang/%s~

LANGUAGE ~American English~
         ~english~
         ~kiara-zaiya/lang/english/dialogs.tra~
         ~kiara-zaiya/lang/english/kzsetup.tra~

LANGUAGE ~Francais (par Elgaern, Bloody.Mary et Lothringen des d'Oghmatiques)~
         ~french~
         ~kiara-zaiya/lang/french/dialogs.tra~
         ~kiara-zaiya/lang/french/kzsetup.tra~

LANGUAGE ~Espanol (traduccion por Saemon)~
         ~spanish~
         ~kiara-zaiya/lang/spanish/dialogs.tra~
         ~kiara-zaiya/lang/spanish/kzsetup.tra~

LANGUAGE ~Deutsch (Uebersetzung von Leonardo Watson)~
         ~german~
         ~kiara-zaiya/lang/german/dialogs.tra~
         ~kiara-zaiya/lang/german/kzsetup.tra~

LANGUAGE ~Russian (Gingerrr & aerie.ru. Updated by Prowler)~
         ~russian~
         ~kiara-zaiya/lang/russian/dialogs.tra~
         ~kiara-zaiya/lang/russian/kzsetup.tra~


/* ========================================== *
 *    Kiara-Zaiya for BG2 (main component)    *
 * ========================================== */
BEGIN @1                                 // ~Kiara-Zaiya for BG2~
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @2 // ~You do not appear to have Throne of Bhaal installed.~

OUTER_SET kz_custom = 0

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


/* ============================================================================ *
 *    Kiara-Zaiya mod with custom soundsets (English only)  (main component)    *
 * ============================================================================ */
BEGIN @150                               // ~Kiara-Zaiya for BG2 with custom soundsets (English only)~
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @2 // ~You do not appear to have Throne of Bhaal installed.~

OUTER_SET kz_custom = 1

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


/* ======================================================= *
 *    Alternate Monk High level abilities by TG Maestro    *
 * ======================================================= */
BEGIN @129 // ~Alternate High level abilities by TG Maestro~

// Remove later
COPY ~kiara-zaiya/hla/lumo0.2da~ ~override/LUMO0.2da~

// Shadow Stance!
COPY ~kiara-zaiya/hla/shdst01.spl~ ~override~
	SAY NAME1 @10
	SAY UNIDENTIFIED_DESC @11
// Dragon fist!
COPY ~kiara-zaiya/hla/dragf01.spl~ ~override~
	SAY NAME1 @130
	SAY UNIDENTIFIED_DESC @131
COPY ~kiara-zaiya/hla/dragon1.eff~ ~override~
     ~kiara-zaiya/hla/dragon2.eff~ ~override~
     ~kiara-zaiya/hla/dragon3.eff~ ~override~
     ~kiara-zaiya/hla/dragon4.eff~ ~override~
     ~kiara-zaiya/hla/dragon5.eff~ ~override~
     ~kiara-zaiya/hla/dragon6.eff~ ~override~
     ~kiara-zaiya/hla/dragon7.eff~ ~override~
// Shadowless Kick!
COPY ~kiara-zaiya/hla/shadk01.spl~ ~override~
	SAY NAME1 @132
	SAY UNIDENTIFIED_DESC @133
// Tiger strike!
COPY ~kiara-zaiya/hla/tiger01.spl~ ~override~
	SAY NAME1 @134
	SAY UNIDENTIFIED_DESC @135
// Chant!
COPY ~kiara-zaiya/hla/chant02.spl~ ~override~
	SAY NAME1 @136
	SAY UNIDENTIFIED_DESC @137
// Faster than the eye!
COPY ~kiara-zaiya/hla/faster2.spl~ ~override~
  SAY NAME1 @138
  SAY UNIDENTIFIED_DESC @140
// Solar Stance!
COPY ~kiara-zaiya/hla/solar01.spl~ ~override~
  SAY NAME1 @9
  SAY NAME2 @9
  SAY UNIDENTIFIED_DESC @141


/* =================================================== *
 *    Original Amaralis battles in chapters 2 and 6    *
 * =================================================== */
BEGIN @143 // ~Original Amaralis battles~
REQUIRE_FILE ~Data/25Dialog.bif~ @2

// Amaralis
COPY ~kiara-zaiya\amaralis\c6ama.cre~ ~override~
	SAY NAME1 @47
	SAY NAME2 @47
// Horth'Kar
COPY ~kiara-zaiya\amaralis\c6hroth.cre~ ~override~
	SAY NAME1 @50
	SAY NAME2 @50
// Fenilla
COPY ~kiara-zaiya\amaralis\c6fenil.cre~ ~override~
	SAY NAME1 @144
	SAY NAME2 @144
// Morticia
COPY ~kiara-zaiya\amaralis\c6mortic.cre~ ~override~
	SAY NAME1 @145
	SAY NAME2 @145
// Polgar
COPY ~kiara-zaiya\amaralis\c6polgar.cre~ ~override~
	SAY NAME1 @146
	SAY NAME2 @146
// Keria
COPY ~kiara-zaiya\amaralis\c6keria.cre~ ~override~
	SAY NAME1 @147
	SAY NAME2 @147

COMPILE ~kiara-zaiya\amaralis\amafight.baf~
        ~kiara-zaiya\amaralis\amafigt1.baf~
        ~kiara-zaiya\amaralis\morticia.baf~
        ~kiara-zaiya\amaralis\c6drud.baf~
        ~kiara-zaiya\amaralis\polgar.baf~
        ~kiara-zaiya\amaralis\keriafit.baf~


EXTEND_BOTTOM ~ar1700.bcs~ ~kiara-zaiya/amaralis/a2ma1700.baf~


/* ================================================================== *
 *    Tougher Kiara vampire                                           *
 *                                                                    *
 *  MONK LEVEL 20, DOES LEVEL DRAIN AND HAS SOME OTHER UNDEAD POWERS  *
 * ================================================================== */
BEGIN @261 // ~Tougher Kiara vampire~
REQUIRE_FILE ~Data/25Dialog.bif~ @2

COMPILE ~kiara-zaiya/vampkia/vampkia.baf~
COPY ~kiara-zaiya/vampkia/vampkiar.cre~ ~override~
	SAY NAME1 @49
	SAY NAME2 @49
COPY ~kiara-zaiya/vampkia/vampk3.itm~ ~override~


/* =========================== *
 *    Revised Suldalanessar    *
 * =========================== */
BEGIN @262 // ~Revised Suldalanessar~
REQUIRE_FILE ~Data/25Dialog.bif~ @2

// Isaya's note:
// although Hoppy suggested to use the demon lord only if Kiara and Zaiya were in the party
// ( http://www.shsforums.net/topic/43574-suldanesselar-bugs/#entry470369 ),
// I'll leave the demon lord as it is. If the player doesn't want the demon lord, the best way
// is to avoid installing this component. Why skipping the demon lord only and not the other changes
// brought to Suldalanessar?

// RAKSHASA GUARDING TEMPLE - GOOD FIGHT
COPY ~kiara-zaiya/sulda/kz#rakma.cre~ ~override~
     ~kiara-zaiya/sulda/plyuddea.eff~ ~override~
COMPILE ~kiara-zaiya/sulda/kz#rakma.baf~
        ~kiara-zaiya/sulda/spwnrak.baf~

// DEMON IN REPLACEMENT OF NIMZI: EXTREMELY TOUGH BUT FEASIBLE
// BWP fix: Lollorian's patched Revised Suldanessalar
//COPY ~kiara-zaiya/sulda/AR2807.are~ ~override~
//COPY ~kiara-zaiya/sulda/AR2807.wed~ ~override~
COPY_EXISTING ~ar2807.are~ ~override~
	LPF ~ALTER_AREA_ACTOR~ STR_VAR actor_name = ~Black Dragon~ cre_file = ~KZ#DMLRD~ END
BUT_ONLY

COPY ~kiara-zaiya/sulda/kz#dmlrd.cre~ ~override~

COPY ~kiara-zaiya/sulda/kzldlpo1.spl~ ~override~ // male polymorph into demon knights no save
     ~kiara-zaiya/sulda/kzldlpol.spl~ ~override~ // female polymorph into succubus no save
	SAY NAME1 @263
COMPILE ~kiara-zaiya/sulda/kz#dmlrd.baf~
// BWP fix: Lollorian's patched Revised Suldanessalar
//      ~kiara-zaiya/sulda/uddeath.baf~
//      ~kiara-zaiya/sulda/demsuc.baf~
EXTEND_TOP ~uddeath.bcs~ ~kiara-zaiya/sulda/uddeath.baf~
EXTEND_TOP ~demsuc.bcs~  ~kiara-zaiya/sulda/demsuc.baf~

// A LICH AND TWO VAMPIRE ILITHID INSTEAD OF UNDEAD = EASY
// BWP fix: Lollorian's patched Revised Suldanessalar
//COMPILE ~kiara-zaiya/sulda/suscene1.baf~
COPY_EXISTING ~suscene1.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~CreateCreature("SUUNDEAD",\[1259\.2912\],8)~ ~CreateCreature("LICH01",[1259.2912],8)~
		REPLACE_TEXTUALLY ~CreateCreature("SUUNDEAD",\[1394\.2900\],8)~ ~CreateCreature("KZ#FVAMP",[1394.2900],8)~
		REPLACE_TEXTUALLY ~CreateCreature("SUUNDEAD",\[1132\.2892\],8)~ ~CreateCreature("KZ#FVAMP",[1132.2892],8)~
	END
BUT_ONLY
COPY ~kiara-zaiya/sulda/kz#fvamp.cre~ ~override~
COMPILE ~kiara-zaiya/sulda/kz#flayu.baf~

// MARILITH INSTEAD OF TROLLS = EASY
// BWP fix: Lollorian's patched Revised Suldanessalar
// COMPILE ~kiara-zaiya/sulda/suscene3.baf~
COPY_EXISTING ~suscene3.bcs~ ~override~
	DECOMPILE_AND_PATCH BEGIN
		REPLACE_TEXTUALLY ~CreateCreature("SUTROLL",\[3549\.3349\],8)~ ~CreateCreature("DEMOSUM1",[3549.3349],8)~
		REPLACE_TEXTUALLY ~CreateCreature("SUTROLL",\[3437\.3388\],8)~ ~CreateCreature("DEMOSUM1",[3437.3388],8)~
	END
BUT_ONLY

// ASSASSIN-MAGE NEAR THE DRAGON - NOW REPLACED WITH DEMON!
//COPY ~kiara-zaiya/sulda/kzninja.cre~ ~override~
//COPY ~kiara-zaiya/sulda/Kzninja.itm~ ~override~
//COPY ~kiara-zaiya/sulda/kzninja.bcs~ ~override~
//REPLACE ~99999~ ~Irenicus said they would come for the cup. I shall not fail my master.~
//EXTEND_TOP ~ar2807.bcs~ ~kiara-zaiya/sulda/kz#2807a.baf~

COPY ~kiara-zaiya/sulda/kzninja.cre~ ~override~
COPY ~kiara-zaiya/sulda/kzninja.itm~ ~override~
COMPILE ~kiara-zaiya/sulda/kzninja.baf~
EXTEND_TOP ~ar2807.bcs~ ~kiara-zaiya/sulda/kz#2807a.baf~


/* =========================== *
 *    Jao and party for SOA    *
 * =========================== */
BEGIN @250 // ~Jao and party for SOA: another tough fight~
REQUIRE_FILE ~Data/25Dialog.bif~ @2

EXTEND_TOP ~ar0300.bcs~ ~kiara-zaiya/wu-je/appendme.baf~ EVALUATE_BUFFER
COPY ~kiara-zaiya/wu-je/kzjao.cre~ ~override~
	SAY NAME1 @251
	SAY NAME2 @251
COPY ~kiara-zaiya/wu-je/kzwuje.cre~ ~override~
	SAY NAME1 @253
	SAY NAME2 @253
// Isaya: dialog "traified" and forced to use the appropriate translated tra file (instead of KZsetup.tra and Dialogs.tra)
// Gwendolyne: Useless with AUTO_TRA
COMPILE ~kiara-zaiya/wu-je/kzjao1.d~ // USING ~kiara-zaiya/lang/%s/kzjao1.tra~
COMPILE ~kiara-zaiya/wu-je/kzjao.baf~
        ~kiara-zaiya/wu-je/kzwuje.baf~
COPY ~kiara-zaiya/wu-je/kzbrac1.itm~ ~override~
	SAY NAME1 @254
	SAY NAME2 @254
	SAY DESC @255


/* ========================================== *
 *    Tougher Irenicus at the Tree of life    *
 * ========================================== */
BEGIN @260 // ~Tougher Irenicus at the Tree of life~
REQUIRE_FILE ~Data/25Dialog.bif~ @2

// TOUGH BUT STILL FEASIBLE
COPY ~kiara-zaiya/irentree/sujon.cre~ ~override~
COMPILE ~kiara-zaiya/irentree/sujon.baf~
