//////////////////////////////////////////////////////////////////
//JAO - MAGE 24TH LEVEL
//IMMUNE TO PHYSICAL AND MAGICAL DAMAGE
//CAN BE KILLED THOUGH WITH LUCK!
/////////////////////////////////////////////////////////////////

//REINITIALIZE JAO IF PARTY RESTED
IF
  Global("WUJESTART","GLOBAL",1)
  GlobalTimerExpired("JaoRestT","GLOBAL")
THEN
  RESPONSE #100
    SetGlobal("Prep","LOCALS",0)
    SetGlobal("Prep1","LOCALS",0)
    SetGlobal("Prep2","LOCALS",0)
    SetGlobal("TRAPS","LOCALS",0)
    SetGlobal("InsideStopTime","LOCALS",0)
    SetGlobal("KZABI","LOCALS",0)
    SetGlobal("KZBLIND","LOCALS",0)
    SetGlobal("DispelSecondMage","LOCALS",0)
    SetGlobal("Dispel1SecondMage","LOCALS",0)
    SetGlobal("SpellTriger","LOCALS",0)
    Rest()
    Wait(1)
    Continue()
END

//PRE-FIGHT BUFFING AS ANY PARTY SHOULD
IF
  OR(3)
    InMyArea(Player1)
    Detect([PC])
    See([PC])
  Global("Prep1","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobalTimer("JaoRestT","GLOBAL",SIX_HOURS)
    ReallyForceSpell(Myself,CLERIC_HEAL)
    ReallyForceSpell(Myself,WIZARD_STONE_SKIN)
    ReallyForceSpell(Myself,WIZARD_TRUE_SIGHT)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_MAGIC_ENERGY)
    ReallyForceSpell(Myself,WIZARD_SPELL_SHIELD)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_FIRE)
    ReallyForceSpell(Myself,WIZARD_PROTECTION_FROM_ACID)
    SetGlobal("Prep1","LOCALS",1)
    Wait(1)
    Continue()
END

//Move to player1
IF
  Global("WUJESTART","GLOBAL",0)
  OR(2)
   See([PC])
   Detect([PC]) 
  Allegiance(Myself,NEUTRAL)
  !Range(Player1,5)
THEN
  RESPONSE #100
    MoveToObject(Player1)
END


//INITIATE DIALOG
IF
  Global("WUJESTART","GLOBAL",0)
  OR(2)
   See([PC])
   Detect([PC]) 
  Allegiance(Myself,NEUTRAL)
  Range(Player1,8)
THEN
  RESPONSE #100
 	StartDialogNoSet([PC])
END

//IF I HAVENT SPOKEN TO THE PC THEN CPU SAVE

//CONTINGENCY - SMALL AND BIG
IF
  !GlobalTimerExpired("JaoRestT","LOCALS")
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",0)
  Range(NearestEnemyOf(Myself),120)
  Detect(NearestEnemyOf(Myself))
  !Allegiance(NearestEnemyOf(Myself),ENEMY)
THEN
  RESPONSE #100
    ChangeClass(Myself,MAGE)
    SetGlobalTimer("CastSpell","LOCALS",ONE_ROUND)
    SetGlobalTimer("InsideStopTime","LOCALS",FOUR_ROUNDS)
    SetGlobalTimer("TRAPS","LOCALS",TWO_TURNS)
    SetGlobal("Prep","LOCALS",1)
    DisplayString(Myself,40252)
    ReallyForceSpell(Myself,WIZARD_SPELL_TRAP)
    ReallyForceSpell(Myself,WIZARD_TIME_STOP)
    ReallyForceSpell(Myself,WIZARD_IMPROVED_ALUCRITY)
END

//Mordenkainen may be too log with energy drain
IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
 Global("MordyAttack","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("MordyAttack","LOCALS",1)
    Spell(Myself,WIZARD_MORDENKAINENS_SWORD)
END

//Dispel protection - spellstrike
IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
  Global("DispelSecondMage","LOCALS",0)
  Detect(NearestEnemyOfType([PC.0.0.MAGE_ALL]))
  OR(3)
    CheckStatGT(NearestEnemyOf([PC.0.0.MAGE_ALL]),0,WIZARD_SPELL_TRAP)
    CheckStatGT(NearestEnemyOf([PC.0.0.MAGE_ALL]),1,WIZARD_SPELL_TURNING)
    CheckStatGT(NearestEnemyOf([PC.0.0.MAGE_ALL]),1,WIZARD_SPELL_DEFLECTION)
  HaveSpell(WIZARD_SPELL_STRIKE)
THEN
  RESPONSE #100
    SetGlobal("DispelSecondMage","LOCALS",1)
    Spell(LastSeenBy(Myself),WIZARD_SPELL_STRIKE)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
  Global("Dispel1SecondMage","LOCALS",0)
  Detect(NearestEnemyOfType([PC.0.0.MAGE_ALL]))
  OR(2)
    CheckStatGT(NearestEnemyOf([PC.0.0.MAGE_ALL]),0,STONESKINS)
    CheckStatGT(NearestEnemyOf([PC.0.0.MAGE_ALL]),1,WIZARD_PROTECTION_FROM_MAGIC_WEAPONS)
  HaveSpell(WIZARD_BREACH)
THEN
  RESPONSE #100
    SetGlobal("Dispel1SecondMage","LOCALS",1)
    Spell(LastSeenBy(Myself),WIZARD_BREACH)
END

//////////////////////////////////////////////////////////////
//SPELL TRIGGER DISPEL-MALISON-CHAOS
//////////////////////////////////////////////////////////////

IF
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  OR(7)
    See(NearestEnemyOfType([PC.0.0.CLERIC_ALL]))
    See(NearestEnemyOfType([PC.0.0.DRUID_ALL]))
    See(NearestEnemyOfType([PC.0.0.THIEF_ALL]))
    See(NearestEnemyOfType([PC.0.0.FIGHTER_ALL]))
    See(NearestEnemyOfType([PC.0.0.RANGER_ALL]))
    See(NearestEnemyOfType([PC.0.0.PALADIN_ALL]))
    See(NearestEnemyOfType([PC.0.0.BARD_ALL]))
  Global("SpellTrigger","LOCALS",0)
  !StateCheck(Myself,STATE_REALLY_DEAD)
THEN
  RESPONSE #100
    DisplayString(Myself,39968)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_DISPEL_MAGIC)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_GREATER_MALISON)
    ReallyForceSpell(LastSeenBy(Myself),WIZARD_CHAOS)
    SetGlobal("SpellTrigger","LOCALS",1)
END

//////////////////////////////////////////////////////////////
//LETS REMOVE 18TH LEVEL FROM THE MAGE
//////////////////////////////////////////////////////////////

IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
  Global("ENDRAIN","LOCALS",0)
  See(NearestEnemyOfType([PC.0.0.MAGE_ALL]))
  HaveSpell(WIZARD_ENERGY_DRAIN)
THEN
  RESPONSE #100
    SetGlobal("ENDRAIN","LOCALS",1)
    Spell(LastSeenBy(Myself),WIZARD_ENERGY_DRAIN)
    ApplySpell(LastSeenBy(Myself),WIZARD_ENERGY_DRAIN)
    ApplySpell(LastSeenBy(Myself),WIZARD_ENERGY_DRAIN)
END

//////////////////////////////////////////////////////////////
//SECOND TIME STOP
//////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
  Global("KZtimestop","LOCALS",0)
  HaveSpell(WIZARD_TIME_STOP)
THEN
  RESPONSE #100
    SetGlobalTimer("InsideStopTime","LOCALS",FOUR_ROUNDS)
    SetGlobal("KZtimestop","LOCALS",1)
    Spell(Myself,WIZARD_TIME_STOP)
END

//////////////////////////////////////////////////////////////

//abi dazim
IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
  Global("KZABI","LOCALS",0)
  OR(2)
  See(NearestEnemyOfType([PC.0.0.CLERIC_ALL]))
  Detect(NearestEnemyOfType([PC.0.0.CLERIC_ALL]))
  !HasItemEquiped("Clck26",NearestEnemyOfType([PC.0.0.CLERIC_ALL]))  
  !HasItemEquiped("Belt10",NearestEnemyOfType([PC.0.0.CLERIC_ALL]))
THEN
  RESPONSE #100
    SetGlobal("KZABI","LOCALS",1)
    Spell(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
  Global("KZABI","LOCALS",0)
  OR(2)
  See(NearestEnemyOfType([PC.0.0.DRUID_ALL]))
  Detect(NearestEnemyOfType([PC.0.0.DRUID_ALL]))
  !HasItemEquiped("Clck26",NearestEnemyOfType([PC.0.0.DRUID_ALL]))  
  !HasItemEquiped("Belt10",NearestEnemyOfType([PC.0.0.DRUID_ALL]))
THEN
  RESPONSE #100
    SetGlobal("KZABI","LOCALS",1)
    Spell(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
  Global("KZABI","LOCALS",0)
  OR(2)
  See(NearestEnemyOfType([PC.0.0.THIEF_ALL]))
  Detect(NearestEnemyOfType([PC.0.0.THIEF_ALL]))
  !HasItemEquiped("Clck26",NearestEnemyOfType([PC.0.0.THIEF_ALL]))  
  !HasItemEquiped("Belt10",NearestEnemyOfType([PC.0.0.THIEF_ALL]))
THEN
  RESPONSE #100
    SetGlobal("KZABI","LOCALS",1)
    Spell(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

//////////////////////////////////////////////////////////////
//BLINDNESS ON NEAREST FIGHTER
//////////////////////////////////////////////////////////////
IF
  Allegiance(Myself,ENEMY)
  Global("Prep","LOCALS",1)
  Global("KZBLIND","LOCALS",0)
  OR(4)
    See(NearestEnemyOfType([PC.0.0.MONK]))
    See(NearestEnemyOfType([PC.0.0.FIGHTER_ALL]))
    See(NearestEnemyOfType([PC.0.0.PALADIN_ALL]))
    See(NearestEnemyOfType([PC.0.0.RANGER_ALL]))
THEN
  RESPONSE #100
    SetGlobal("KZBLIND","LOCALS",1)
    Spell(LastSeenBy(Myself),WIZARD_BLINDNESS)
END

IF
  Allegiance(Myself,ENEMY)
  StateCheck(Myself,STATE_SILENCED)
THEN
  RESPONSE #100
    ReallyForceSpell(Myself,WIZARD_VOCALIZE)
END

//STARTING PROTECTIONS INGAME
IF
  !GlobalTimerExpired("JaoRestT","LOCALS")
  GlobalTimerExpired("TRAPS","LOCALS")
  Allegiance(Myself,ENEMY)
  !StateCheck(Myself,STATE_REALLY_DEAD)
  CheckStatLT(Myself,1,WIZARD_SPELL_TRAP) 
  HaveSpell(WIZARD_SPELL_TRAP)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    SetGlobalTimer("TRAPS","LOCALS",TWO_TURNS)
    Spell(Myself,WIZARD_SPELL_TRAP)
END

//IMMUNE TO FINGER OF DEATH
IF
  !GlobalTimerExpired("JaoRestT","LOCALS")
  GlobalTimerExpired("TRAPS","LOCALS")
  Allegiance(Myself,ENEMY)
  Detect(NearestEnemyOf(Myself))
  CheckStatLT(Myself,0,WIZARD_SPELL_IMMUNITY)
  HaveSpell(WIZARD_SPELL_IMMUNITY)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    SetGlobalTimer("TRAPS","LOCALS",TWO_TURNS)
    DisplayString(Myself,31247)
    Spell(Myself,WIZARD_SPELL_IMMUNITY_NECROMANCY)
END

IF
  !GlobalTimerExpired("JaoRestT","LOCALS")
  GlobalTimerExpired("TRAPS","LOCALS")
  Allegiance(Myself,ENEMY)
  HaveSpell(WIZARD_MINOR_SPELL_TURNING)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    SetGlobalTimer("TRAPS","LOCALS",ONE_TURN)
    Spell(Myself,WIZARD_MINOR_SPELL_TURNING)
END

IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  Detect(NearestEnemyOf(Myself))
  Global("Sequencer","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobalTimer("Cast","LOCALS",ONE_ROUND)
    SetGlobal("Sequencer","LOCALS",1)
    DisplayString(Myself,25951)
    ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_RED)
    ReallyForceSpell(Myself,WIZARD_FIRE_SHIELD_BLUE)
    DisplayString(Myself,31247)
    ReallyForceSpell(Myself,WIZARD_SPELL_IMMUNITY_ABJURATION)
END

//DID PLAYER ONE SUMMMON SOME ANIMALS?
IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  See(NearestEnemyOfType([GOODCUTOFF.0.0.0.0.SUMMONED]))
  HaveSpell(WIZARD_DEATH_FOG)
THEN
  RESPONSE #1
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),WIZARD_DEATH_FOG)
END

/////////////////////////////////////////////////////////////
//CORE OF THE FIGHT
//BRING SOME HELP FIRST! PLANATAR AND MORDY
/////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !GlobalTimerExpired("KeriaRestT","LOCALS")
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MORDENKAINENS_SWORD)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_MORDENKAINENS_SWORD)
END


IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !GlobalTimerExpired("KeriaRestT","LOCALS")
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_SUMMON_PLANATAR_EVIL)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(Myself,WIZARD_SUMMON_PLANATAR_EVIL)
END

///////////////////////////////////////////////////////////////////
//NUKE THEM
///////////////////////////////////////////////////////////////////
IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  OR(7) 
    See(NearestEnemyOfType([PC.0.0.FIGHTER_ALL]))
    See(NearestEnemyOfType([PC.0.0.CLERIC_ALL]))
    See(NearestEnemyOfType([PC.0.0.DRUID_ALL]))
    See(NearestEnemyOfType([PC.0.0.THIEF_ALL]))
    See(NearestEnemyOfType([PC.0.0.BARD_ALL]))
    See(NearestEnemyOfType([PC.0.0.PALADIN_ALL]))
    See(NearestEnemyOfType([PC.0.0.RANGER_ALL]))
  !GlobalTimerExpired("KeriaRestT","LOCALS")
  !Allegiance(LastSeenBy(Myself),ENEMY)
  CheckStatLT(LastSeenBy(Myself),50,MAGICDAMAGERESISTANCE)
  !CheckStat(LastSeenBy(Myself),7,186)
  !HasItemEquiped("CLCK26",LastSeenBy(Myself))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  HaveSpell(WIZARD_ABI_DALZIMS_HORRID_WILTING)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),WIZARD_ABI_DALZIMS_HORRID_WILTING)
END

/////////////////////////////////////////////////////////////////
//TARGETED KILL
/////////////////////////////////////////////////////////////////

IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  !GlobalTimerExpired("KeriaRestT","LOCALS")
  !Allegiance(LastSeenBy(Myself),ENEMY)
  OR(7) 
    See(NearestEnemyOfType([PC.0.0.FIGHTER_ALL]))
    See(NearestEnemyOfType([PC.0.0.CLERIC_ALL]))
    See(NearestEnemyOfType([PC.0.0.DRUID_ALL]))
    See(NearestEnemyOfType([PC.0.0.THIEF_ALL]))
    See(NearestEnemyOfType([PC.0.0.BARD_ALL]))
    See(NearestEnemyOfType([PC.0.0.PALADIN_ALL]))
    See(NearestEnemyOfType([PC.0.0.RANGER_ALL]))
  !Gender(LastSeenBy(Myself),SUMMONED)
  !CheckStat(LastSeenBy(Myself),7,186)
  CheckStat(LastSeenBy(Myself),0,SCRIPTINGSTATE2)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  !HasItemEquiped("CLCK26",LastSeenBy(Myself))
  CheckStat(LastSeenBy(Myself),0,173)
  HaveSpell(WIZARD_FINGER_OF_DEATH)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),WIZARD_FINGER_OF_DEATH)
END

IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  !GlobalTimerExpired("KeriaRestT","LOCALS")
  OR(7) 
    See(NearestEnemyOfType([PC.0.0.FIGHTER_ALL]))
    See(NearestEnemyOfType([PC.0.0.CLERIC_ALL]))
    See(NearestEnemyOfType([PC.0.0.THIEF_ALL]))
    See(NearestEnemyOfType([PC.0.0.DRUID_ALL]))
    See(NearestEnemyOfType([PC.0.0.BARD_ALL]))
    See(NearestEnemyOfType([PC.0.0.PALADIN_ALL]))
    See(NearestEnemyOfType([PC.0.0.RANGER_ALL]))
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !CheckStat(LastSeenBy(Myself),7,186)
   CheckStat(LastSeenBy(Myself),0,SCRIPTINGSTATE2)
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGIC)
  !HasItemEquiped("CLCK26",LastSeenBy(Myself))
  CheckStat(LastSeenBy(Myself),0,173)
  HaveSpell(WIZARD_FLESH_TO_STONE)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(LastSeenBy(Myself),WIZARD_FLESH_TO_STONE)
END

IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !GlobalTimerExpired("KeriaRestT","LOCALS")
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_CHAOS)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(NearestEnemyOf(Myself),WIZARD_CHAOS)
END

IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !GlobalTimerExpired("KeriaRestT","LOCALS")
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_SLOW)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(NearestEnemyOf(Myself),WIZARD_SLOW)
END

IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  !HasBounceEffects(LastSeenBy(Myself))
  CheckStatLT(LastSeenBy(Myself),50,RESISTACID)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !GlobalTimerExpired("KeriaRestT","LOCALS")
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MELF_ACID_ARROW)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(NearestEnemyOf,WIZARD_MELF_ACID_ARROW)
END

IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  !Allegiance(LastSeenBy(Myself),ENEMY)
  Allegiance(Myself,ENEMY)
  !HasBounceEffects(LastSeenBy(Myself))
  CheckStatLT(LastSeenBy(Myself),50,RESISTMAGICFIRE)
  !GlobalTimerExpired("KeriaRestT","LOCALS")
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_FLAME_ARROW)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(NearestEnemyOf(Myself),WIZARD_FLAME_ARROW)
END

IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !HasBounceEffects(LastSeenBy(Myself))
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_BLINDNESS)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(NearestEnemyOf(Myself),WIZARD_BLINDNESS)
END

IF
  GlobalTimerExpired("CASTSPELL","LOCALS")
  Allegiance(Myself,ENEMY)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  !HasBounceEffects(LastSeenBy(Myself))
  See(NearestEnemyOf(Myself))
  HaveSpell(WIZARD_MAGIC_MISSILE)
THEN
  RESPONSE #100
    SetGlobalTimer("CASTSPELL","LOCALS",ONE_ROUND)
    Spell(NearestEnemyOf(Myself),WIZARD_MAGIC_MISSILE)
END

///////////////////////////////////////////////////////////////////
//ATTACK SINCE IMMUNE
///////////////////////////////////////////////////////////////////

//DO NOT GET INVISIBLE EACH TIME
IF
  !GlobalTimerExpired("InsideStopTime","LOCALS")
  !GlobalTimerExpired("CASTSPELL","LOCALS")
  !HasItem("melfmet",Myself)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  OR(3)
    See(NearestEnemyOf(Myself))
    See(WorstAC)
    See(MostDamagedOf(Myself))
  !GlobalTimerExpired("Cast","LOCALS")
  ActionListEmpty()
  Global("EquipStaff","LOCALS",0)
THEN
  RESPONSE #100
    SetGlobal("EquipStaff","LOCALS",1)
    EquipMostDamagingMelee()
    AttackReevaluate(LastSeenBy(Myself),15)
END

IF
  !GlobalTimerExpired("InsideStopTime","LOCALS")
  !GlobalTimerExpired("CASTSPELL","LOCALS")
  !HasItem("melfmet",Myself)
  !Allegiance(LastSeenBy(Myself),ENEMY)
  OR(3)
    See(NearestEnemyOf(Myself))
    See(WorstAC)
    See(MostDamagedOf(Myself))
  !GlobalTimerExpired("Cast","LOCALS")
  ActionListEmpty()
THEN
  RESPONSE #100
    AttackReevaluate(LastSeenBy(Myself),15)
END


//MOVE CLOSER IF THEY DISAPPEARED
IF
  Allegiance(Myself,ENEMY)
  !Range([PC],39)//spell range 40
  ActionListEmpty()
THEN
  RESPONSE #100
    MoveToObject(Player1)
END
